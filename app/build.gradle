apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'

repositories {
    mavenCentral()
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "com.ladwa.aditya.twitone"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"
        // Enabling multidex support.
        multiDexEnabled true
        //Enable AndroidJUnitRunner as default Instrumentation Runner
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }


    dexOptions {
        maxProcessCount 2
        javaMaxHeapSize "6g"

    }

    buildTypes {
        release {
            minifyEnabled true     // Minify enabled to true for shrink resources to work
            shrinkResources true
            // Remove unused resources from project and other libraries. Greatly reduce apk size
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix ".debug"
        }
    }

    //Resolve Conflicts
    packagingOptions {
        exclude 'LICENSE.txt'
        exclude 'META-INF/maven/com.google.guava/guava/pom.properties'
        exclude 'META-INF/maven/com.google.guava/guava/pom.xml'
    }
    productFlavors {
        dev {
            minSdkVersion rootProject.ext.minSdkVersionDev
            applicationId 'com.ladwa.aditya.twitone.dev'
            targetSdkVersion 23
            versionCode 1
            versionName '1.0'
        }
        production {
            applicationId "com.ladwa.aditya.twitone"
            minSdkVersion rootProject.ext.minSdkVersion
        }
    }
}

/* Dependency version are defined in top level build.gradle file.This helps keeping track of all versions in s single place. This helps
readability and helps managing project complexity
 */
dependencies {
    // Apps dependencies
    compile deps.appcompactV7
    compile deps.design
    compile deps.multidex

    //ButterKnife
    compile deps.butterKnife
    apt deps.butterKnifeComipler

    //Progress Bars
    compile deps.smoothProgress
    compile deps.materialProgress

    //Mikepenz Material Drawer
    compile('com.mikepenz:materialdrawer:5.3.2@aar') {
        transitive = true
    }
    compile deps.googleMaterial

    //Glide
    compile deps.glide

    //Stetho
    compile 'com.facebook.stetho:stetho:1.3.1'

    //RxJava
    compile deps.rxJava
    compile deps.rxAndroid

    //Dagger
    apt deps.daggerCompiler
    compile deps.dagger
    provided deps.javax

    //Twitter4j
    compile deps.twitter4j
    compile deps.twitter4jStream
    compile deps.twitter4jAsync

    //Timber
    compile deps.timber

    //Leak Canary
    debugCompile deps.leakCanary
    releaseCompile deps.leakCanaryTestAndRelease
    testCompile deps.leakCanaryTestAndRelease

    //Test dependency
    testCompile deps.junit
    androidTestCompile deps.testRunner
    androidTestCompile deps.testRule
    androidTestCompile deps.espresso
    androidTestCompile deps.uiautomator
    androidTestCompile deps.annotation
    androidTestCompile deps.espressoWeb
}

tasks.whenTaskAdded { task ->
    if (task.name.equals("lint")) {
        task.enabled = false
    }
}
