apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'

repositories {
    mavenCentral()
}

android {
    compileSdkVersion 24
    buildToolsVersion '24.0.1'

    defaultConfig {
        applicationId "com.ladwa.aditya.twitone"
        minSdkVersion 15
        targetSdkVersion 24
        versionCode 1
        versionName "1.0"
        // Enabling multidex support.
        multiDexEnabled true
        //Enable AndroidJUnitRunner as default Instrumentation Runner
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }


    dexOptions {
        maxProcessCount 2
        javaMaxHeapSize "6g"

    }

    buildTypes {
        release {
            minifyEnabled true     // Minify enabled to true for shrink resources to work
            shrinkResources true
            // Remove unused resources from project and other libraries. Greatly reduce apk size
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
//            applicationIdSuffix ".debug"
        }
    }

    //Resolve Conflicts
    packagingOptions {
        exclude 'LICENSE.txt'
        exclude 'META-INF/maven/com.google.guava/guava/pom.properties'
        exclude 'META-INF/maven/com.google.guava/guava/pom.xml'
    }
    productFlavors {
        dev {
            minSdkVersion 21
            applicationId 'com.ladwa.aditya.twitone'
            targetSdkVersion 24
            versionCode 1
            versionName '1.0'
        }
        production {
            applicationId "com.ladwa.aditya.twitone"
            minSdkVersion 15
        }
    }
    sourceSets { main { assets.srcDirs = ['src/main/assets', 'src/main/assets/'] } }
}

/* Dependency version are defined in top level build.gradle file.This helps keeping track of all versions in s single place. This helps
   readability and helps managing project complexity
 */
dependencies {
    // Apps dependencies
    compile deps.appcompactV7
    compile deps.design
    compile deps.multidex
    compile deps.cardView
    compile deps.supportv4


    //ButterKnife
    compile deps.butterKnife
    apt deps.butterKnifeComipler

    //Progress Bars
    compile deps.smoothProgress
    compile deps.materialProgress

    //Mikepenz Material Drawer
    compile(deps.materialDrawer) {
        transitive = true
    }

    //Fonts
    compile deps.googleMaterial
    compile deps.fontAwesome

    //Glide
    compile deps.glide



    //Play Services
    compile deps.analytics
    compile deps.location

    //ImageViews
    compile deps.circularImageView
    compile deps.subSamplingImageView

    //Stetho
    compile deps.stetho

    //StorIO
    compile deps.storIOSqlite
    compile deps.storIOContent
    compile deps.storIOSqlAnnotation
    compile deps.storIOContentAnnotation
    apt deps.storIOSqlProcessor
    apt deps.storIOContentProcessor

    //RxJava
    compile deps.rxJava
    compile deps.rxAndroid

    //Dagger
    apt deps.daggerCompiler
    compile deps.dagger
    provided deps.javax

    //Twitter4j
    compile deps.twitter4j
    compile deps.twitter4jStream
    compile deps.twitter4jAsync

    //Timber
    compile deps.timber

    //Leak Canary
    debugCompile deps.leakCanary
    releaseCompile deps.leakCanaryTestAndRelease
    testCompile deps.leakCanaryTestAndRelease

    //Auto value
    compile deps.autoValue
    apt deps.autoValueCompiler
    apt deps.autoParcle

    //Test dependency
    testCompile deps.junit
    androidTestCompile deps.testRunner
    androidTestCompile deps.testRule
    androidTestCompile deps.espresso
    androidTestCompile deps.uiautomator
    androidTestCompile deps.annotation
    androidTestCompile deps.espressoWeb

}

tasks.whenTaskAdded { task ->
    if (task.name.equals("lint")) {
        task.enabled = false
    }
}
apply plugin: 'com.google.gms.google-services'
